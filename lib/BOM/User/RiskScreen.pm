package BOM::User::RiskScreen;

use strict;
use warnings;
use feature qw(state);
no indirect;

=head1 NAME

A business class for the table B<users.riskscreen> in userdb, along with subs for searching in database
and syncing with RiskScreen server.

=head1 DESCRIPTION

Each object of this class represents a summary of user's profile in RiskScreen.

=cut

use LandingCompany::Registry;
use List::Util qw(any none);

use BOM::User;
use BOM::Database::UserDB;

use constant STATUS           => [qw(active disabled requested outdated)];
use constant SCREENING_REASON => ('Payment Agent', 'MT5 Labuan', 'MT5 BVI', 'Affiliate');

=head2 new

Creates a new L<BOM::User::RiskScreen> object by getting the following named arguments:

=over 4

=item * C<binary_user_id> - a user id form B<users.user> database

=item * C<status> - screening status: I<reqested>, I<active> or I<inactive>

=item * C<interface_reference> - (optional) the id shared between our system and RiskScreen (B<loginid>)

=item * C<client_entity_id> - (optional) entity id generated by RiskScreen 

=item * C<date_updated> - (optional) the date the riskscreen match data is updated

=item * C<match_potential_volume> - (optional) number of potential matches

=item * C<match_discounted_volume> - (optional) number of discounted matches

=item * C<match_flagged_volume> - (optional) number of flagged matches

=item * C<flags> - (optional) the list of customers flags

=item * C<custom_text1> - (optional) custom text supported by the API (used by compliance team to add notes/reason for manual screening requests)

=item * C<date_added> - (optional) the date at which the customer is added to RiskScreen.

=back

Returns a list of matching L<BOM::User::RiskScreen> objects

=cut

sub new {
    my ($class, %args) = @_;

    my $object = bless {%args}, $class;
    $object->validate();

    return $object;
}

use constant INTERFACE => qw(
    binary_user_id
    status
    interface_reference
    client_entity_id
    date_updated
    match_potential_volume
    match_discounted_volume
    match_flagged_volume
    flags
    custom_text1
    date_added
);

for my $field (INTERFACE) {
    no strict "refs";
    *{"BOM::User::RiskScreen::$field"} = sub {
        my ($self) = @_;

        return $self->{$field};
    }
}

=head2 validate

Validates a RiskScreen object's attributes.

=cut

sub validate {
    my ($self) = @_;

    for (qw/binary_user_id interface_reference status/) {
        die "$_ is mandatory" unless $self->{$_};
    }

    die "Status '$self->{status}' is invalid." unless any { $self->{status} eq $_ } STATUS->@*;

    die "Invalid user id $self->{binary_user_id}" unless BOM::User->new(id => $self->{binary_user_id});

    die "Invalid screening reason (custom text1): $self->{custom_text1}" unless BOM::User::RiskScreen->validate_custom_text1($self->{custom_text1});

    return 1;
}

=head2 validate_custom_text1

Compares the custom text against the predefined Screening Reasons (undef is accepted).

Returns 1 if acceptable; 0 otherwise.
=cut

sub validate_custom_text1 {
    my ($self, $text) = @_;

    return 1 unless defined $text;

    return 1 if any { $_ eq $text } SCREENING_REASON;

    return 0;
}

=head2 dbic

Gets a connection to user database.

=cut

sub dbic {
    return BOM::Database::UserDB::rose_db()->dbic;
}

=head2 find

Searches the database for B<risk_screen> records by a criteria, provided
by the following named args:

=over 4

=item * C<binary_user_id> - a user id form B<users.user> database

=item * C<client_entity_id> - a client enity id, coming from RiskScreen

=item * C<status> - screening status: I<reqested>, I<active> or I<inactive>

=back

Returns a list of matching L<BOM::User::RiskScreen> objects

=cut

sub find {
    my ($self, %args) = @_;

    my @search_fields = qw/binary_user_id client_entity_id status/;

    die 'Search criteria is empty' unless any { $args{$_} } @search_fields;

    my $rows = dbic->run(
        fixup => sub {
            return $_->selectall_arrayref('select * from users.get_risk_screen(?,?,?)', {Slice => {}}, @args{@search_fields});
        });

    return map { BOM::User::RiskScreen->new(%$_) } @$rows;
}

=head2 save

Saves the current object in database.

=cut

sub save {
    my ($self) = @_;

    return dbic->run(
        fixup => sub {
            return $_->do(
                'select from users.set_risk_screen(?,?,?,?,?,?,?,?,?,?,?)', {},
                $self->binary_user_id,          $self->status,
                $self->client_entity_id,        $self->interface_reference,
                $self->date_updated,            $self->match_potential_volume,
                $self->match_discounted_volume, $self->match_flagged_volume,
                $self->flags,                   $self->custom_text1,
                $self->date_added
            );
        });
}

1;
